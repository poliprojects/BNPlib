model {

for (i in 1:numGroups) {
  for (j in 1:num_samples[i]) {
    y[i, j] ~ dnorm(means[rho[i], s[rho[i], i, j]] , tau)
    for (k in 1:numGroups) {
      s[k, i, j] ~ dcat(weights[k, ])
    }
  }
}

# prior on precision tau
sigma ~ dunif(0, 10)
tau <- 1 / sigma^2

# population level groups
for (i in 1:numGroups) {
  probas[i] <- 1
}

for (i in 1:numGroups) {
  rho[i] ~ dcat(probas[])
}


#### Dirichlet Processes ####
gam ~ dunif(0.5, 5)
sigmaH ~ dunif(0, 3)
tauH <- 1 / sigmaH^2

for (i in 1:numGroups) {
  lambda[i] ~ dunif(0, 4)
  mu0[i] ~ dnorm(0, tauH / lambda[i])
}

# mu0 ~ dnorm(0, tauH / lambda)

# atoms
for (i in 1:numGroups) {
  for (h in 1:H) {
    means[i, h] ~ dnorm(mu0[i], tauH)
  }
}

# weights
for (i in 1:numGroups) {
  for (h in 1:(H-1)) {
    v[i, h] ~ dbeta(1, gam)
  }
}

for (i in 1:numGroups) {
  weights[i, 1] <- v[i, 1]
  for (h in 2 : (H-1)) {
    weights[i, h] <- v[i, h] * (1 - v[i, h-1]) * weights[i, h-1]/v[i, h-1]
  }
  weights[i, H] <- 1 - sum(weights[i, 1:(H-1)])
}

} # end model
