model {

for (i in 1:numGroups) {
  for (j in 1:num_samples[i]) {
    for (k in 1:numGroups) {
      s[k, i, j] ~ dcat(weights[k, ])
    }
    y[i, j] ~ dnorm(means[s[rho[i], i, j]] , tau)
  }
}

# prior on precision tau
sigma ~ dunif(0, 10)
tau <- 1 / sqrt(sigma)


# population level groups
for (i in 1:numGroups) {
  probas[i] <- 1
}

for (i in 1:numGroups) {
  rho[i] ~ dcat(probas[])
}


#### Dirichlet Processes ####
lambda <- 2
gam <- 2.0
sigmaH ~ dunif(0, 3)
tauH <- 1 / sigmaH^2

mu0 ~ dnorm(0, tauH / lambda)

# atoms
for (h in 1:H) {
  means[h] ~  dnorm(mu0, tauH)
}

# weights
for (i in 1:numGroups) {
  for (h in 1:(H-1)) {
    v[i, h] ~ dbeta(1, gam)
  }
  v[i, H] = 1
}

for (i in 1:numGroups) {
  weights[i, 1] <- v[i, 1]
  for (h in 2 : H) {
    weights[i, h] <- v[i, h] * (1 - v[i, h-1]) * weights[i, h-1]/v[i, h-1]
  }
}

} # end model
